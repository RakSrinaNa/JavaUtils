buildscript {
    ext {
        slf4j_version = '2.0.0-alpha1'

        commons_io_version = '2.8.0'
        commons_text_version = '1.9'

        send_notification_version = '0.16.0'
        jna_version = '5.6.0'

        hikari_version = '3.4.3'
        h2_version = '1.4.200'
        mysql_connector_java_version = '8.0.22'
        sqlite_jdbc_version = '3.32.3.2'

        jsoup_version = '1.13.1'
        unirest_version = '3.11.05'
        httpcore_nio_version = '4.4.13'
        jackson_version = '2.11.3'

        jakarta_mail_version = '2.0.0'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.34.0'
    id 'com.novoda.build-properties' version '0.4.1'
    id 'io.freefair.lombok' version '5.3.0' apply false
    id "org.javamodularity.moduleplugin" version "1.7.0" apply false
    id "com.github.breadmoirai.github-release" version '2.2.12'
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or(System.getenv()).or([
                "GITHUB_USER" : "N/A",
                "GITHUB_TOKEN": "N/A"
        ])
    }
}

description = 'RakSrinaNa Utils'

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'org.javamodularity.moduleplugin'

    repositories {
        jcenter()
        mavenCentral()
    }

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_15

    dependencies {
        api("org.slf4j:slf4j-api:${slf4j_version}")
    }

    processResources {
        expand project.properties
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/RakSrinaNa/JavaUtils")
                credentials {
                    username = buildProperties.secrets['GITHUB_USER'].string
                    password = buildProperties.secrets['GITHUB_TOKEN'].string
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}

githubRelease {
    owner = "RakSrinaNa"
    repo = "JavaUtils"
    token = buildProperties.secrets['GITHUB_TOKEN'].string
    tagName = "v${project.version}"
    releaseName = "${project.version}"
}

tasks.withType(Wrapper) {
    gradleVersion = project.wrapper_version
}
