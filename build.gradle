plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.novoda.build-properties' version '0.4.1'
    id 'io.freefair.lombok' version '5.0.1'
    id "org.javamodularity.moduleplugin" version "1.6.0"
    id "java"
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or(System.getenv()).or([
                "GITHUB_USER" : "N/A",
                "GITHUB_TOKEN": "N/A"
        ])
    }
}

description = 'RakSrinaNa Utils'

import org.apache.tools.ant.filters.ReplaceTokens

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: "maven-publish"
    apply plugin: "io.freefair.lombok"
    apply plugin: "org.javamodularity.moduleplugin"

    repositories {
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
        maven {
            url = 'https://jcenter.bintray.com'
        }
        maven {
            url = 'https://maven.mrcraftcod.fr'
        }
    }

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_14

    dependencies {
        api("org.slf4j:slf4j-api:${project.slf4j_version}")
    }

    processResources {
        from 'src/main/resources'
        filter(ReplaceTokens, tokens: [project_version: project.version, project_name: project.name])
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/RakSrinaNa/JavaUtils")
                credentials {
                    username = buildProperties.secrets['GITHUB_USER'].string
                    password = buildProperties.secrets['GITHUB_TOKEN'].string
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}

tasks.withType(Wrapper) {
    gradleVersion = project.wrapper_version
}
